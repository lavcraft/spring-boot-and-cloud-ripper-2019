plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE' apply false
}

apply plugin: 'base'

allprojects {
    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    group 'ru.spring.boot.ripper'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencies {
        //Dependency mmanagement â€” use enforcedPlatform and platform for override existed version or use original when import
        [
          enforcedPlatform('org.springframework.boot:spring-boot-dependencies:2.1.3.RELEASE'),
          enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE"),
          platform('org.junit:junit-bom:5.4.1'), //because junit.org redirect to junit.org/junit5/ :)
        ].each {
            implementation it
            annotationProcessor it
            testAnnotationProcessor it
        }

        constraints {
            ['org.projectlombok:lombok:1.18.2'].each {
                compileOnly it
                annotationProcessor it
                testAnnotationProcessor it
            }

            [
             'io.github.benas:random-beans:3.7.0',
            ].each {
                implementation it
            }

            ['org.mockito:mockito-core:2.22.0',
             'org.mockito:mockito-junit-jupiter:2.22.0',
            ].each {
                testImplementation it
            }
        }

        //declare dependencies
        ['org.projectlombok:lombok',
         'org.springframework.boot:spring-boot-configuration-processor',
        ].each {
            compileOnly it
            testCompileOnly it
            annotationProcessor it
            testAnnotationProcessor it
        }

        implementation 'org.springframework.boot:spring-boot-starter-web',
                'org.springframework.boot:spring-boot-starter-actuator'

        testImplementation 'org.mockito:mockito-core',
                'org.mockito:mockito-junit-jupiter',
                'org.junit.jupiter:junit-jupiter-engine',
                'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
            events 'passed', 'skipped', 'failed'
        }
        reports {
            html.enabled = true
        }
    }
}
